## slm-run --config config.ini -f queries/web/cultural-events-plot.sparql --debug
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX schema: <https://schema.org/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX ggf: <http://ggf.org/>
PREFIX ex: <http://example.org/>

SELECT ?capitalLabel ?url ?eventName ?eventDate ?pos ?durSearch ?durSnap ?requestedModel ?durLLM 
WHERE {
  # 1) European capitals
  SERVICE <https://query.wikidata.org/sparql> {
    SELECT DISTINCT ?capitalLabel WHERE {
      ?country wdt:P31 wd:Q6256 ;        # instance of country
               wdt:P30 wd:Q46 ;          # continent Europe
               wdt:P36 ?capital .        # has capital
      ?capital rdfs:label ?capitalLabel .
      FILTER(LANG(?capitalLabel) = "en")
    } ORDER BY ?capitalLabel  LIMIT 10
  }

  # 2) DuckDuckGo: search cultural events
  BIND(CONCAT("cultural event ", STR(?capitalLabel)) AS ?keywords)
  # Alias SEARCH encapsule duckduckgo.search (défauts: limit=3, region="us-en", safesearch="moderate")
  BIND(ggf:SEARCH(?keywords,3) AS ?gSearch)

  GRAPH ?gSearch {
    ?feed a schema:DataFeed ; schema:dataFeedElement ?item .
    ?item a schema:DataFeedItem ;  schema:item ?page .
    ?page a schema:WebPage  .
    BIND(STR(?page) AS ?url)
    OPTIONAL { ?item schema:position ?pos }
    ?gSearch schema:duration ?durSearch 
  }

  # 3) Snapshot (short text, redaction)
  # Alias SNAP encapsule browser.snapshot (défauts: wait_ms=500, text_max=3000, snippet_only=true, redact_pi=true)
  BIND(ggf:SNAP(?url) AS ?gSnap)

  GRAPH ?gSnap {
    ?page2 a schema:WebPage ; schema:url ?url ; schema:text ?text .
    ?gSnap schema:duration ?durSnap 
  }

  # 4) LLM extraction (GROQ) to JSON-LD Event { name, startDate }
  BIND(CONCAT(
    """
    From the following English page text, extract cultural event details if present.
    Return ONLY a JSON-LD object with the exact shape below (omit extra text, no prose):
    {
      "@context": "https://schema.org/",
      "@type": "Event",
      "name": "...",
      "startDate": "YYYY-MM-DD"
    }

    Text (English):
    <page>
    """,
    SUBSTR(?text,1,3000),
    """
    </page>
    """
  ) AS ?prompt)

  # models to test
  VALUES ?requestedModel {
    "openai/gpt-oss-20b"
#    "llama-3.1-8b-instant"
    "llama-3.3-70b-versatile"
    "openai/gpt-oss-120b"
  }

  # LLM extraction via alias function (wrapper around MCP Groq tool)
  BIND(ggf:LLM(?prompt, ?requestedModel) AS ?gLLM)

  GRAPH ?gLLM {
    ?root a schema:Event .
    OPTIONAL { ?root schema:name ?eventName }
    OPTIONAL { ?root schema:startDate ?eventDate }
   ?gLLM schema:duration ?durLLM .
  }

}
ORDER BY ?capitalLabel ?pos