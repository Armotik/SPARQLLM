## slm-run --config config.ini -f queries/web/nantes-cultural-events.sparql --debug
##
## Pipeline:
## 1) Récupère les capitales européennes (Wikidata)
## 2) Pour chaque capitale, lance une recherche DuckDuckGo d'événements culturels
## 3) Snapshotte chaque page trouvée (texte court)
## 4) Extrait avec LLM (GROQ) les événements: name + startDate
##
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX schema: <https://schema.org/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX slm: <http://example.org/>
PREFIX ex: <http://example.org/>

SELECT ?capitalLabel ?url ?eventName ?eventDate ?score WHERE {
  # 1) Capitales européennes
  SERVICE <https://query.wikidata.org/sparql> {
    SELECT ?capital ?capitalLabel WHERE {
      ?country wdt:P31 wd:Q6256 ;        # instance of country
               wdt:P30 wd:Q46 ;          # continent Europe
               wdt:P36 ?capital .        # has capital
      ?capital rdfs:label ?capitalLabel .
      FILTER(LANG(?capitalLabel) = "fr")
    }
    LIMIT 5
  }

  # 2) DDG: recherche d'événements culturels
  BIND(CONCAT("événement culturel ", STR(?capitalLabel)) AS ?keywords)
  BIND(
    ex:SLM-MCP-TOOL(
      "duckduckgo","duckduckgo.search",
      ex:SLM-OBJ(
        "query", ?keywords,
        "limit", 3,
        "region", "fr-fr",
        "safesearch", "moderate"
      )
    ) AS ?gSearch
  )

  GRAPH ?gSearch {
    ?feed a schema:DataFeed ; schema:dataFeedElement ?item .
    ?item a schema:DataFeedItem ;  schema:item ?page .
    ?page a schema:WebPage  .
    BIND(STR(?page) AS ?url)
    OPTIONAL { ?item schema:position ?pos }
  }

  # 3) Snapshot (texte court, redaction)
  BIND(
    ex:SLM-MCP-TOOL(
      "browser","browser.snapshot",
      ex:SLM-OBJ(
        "url", ?url,
        "wait_ms", 500,
        "text_max", 3000,
        "snippet_only", true,
        "redact_pi", true
      )
    ) AS ?gSnap
  )

  GRAPH ?gSnap {
    ?page2 a schema:WebPage ; schema:url ?url ; schema:text ?text .
  }

  # 4) Extraction LLM (GROQ) en JSON-LD d'un Event { name, startDate }
  BIND(CONCAT(
    """
    From the following French page text, extract the cultural event details if present.
    Return ONLY a JSON-LD object with the exact shape below (omit extra text):
    {
      "@context": "https://schema.org/",
      "@type": "Event",
      "name": "...",
      "startDate": "YYYY-MM-DD"
    }

    Text (French):
    <page>
    """,
    SUBSTR(?text,1,1800),
    """
    </page>
    """
  ) AS ?prompt)

  BIND(ex:SLM-LLMGRAPH_GROQ(?prompt, ?url) AS ?gLLM)

  GRAPH ?gLLM {
    ?root a schema:Event ;
          schema:name ?eventName ;
          schema:startDate ?eventDate .
  }

  # Score optionnel depuis DDG
  OPTIONAL {
    GRAPH ?gSearch { ?item schema:position ?score }
  }
}
ORDER BY ?capitalLabel ?score