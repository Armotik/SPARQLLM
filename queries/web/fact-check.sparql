# slm-run --config config.ini -f queries/web/fact-check.sparql --debug

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dbo:  <http://dbpedia.org/ontology/>
PREFIX schema: <https://schema.org/>
PREFIX slm: <http://slm.local/>
PREFIX ex: <http://example.org/>

SELECT ?countryLabel ?capitalLabel ?url ?title ?decision ?ratingValue ?evidence WHERE {
  # 1) DBpedia: (pays, capitale)
  SERVICE <https://dbpedia.org/sparql> {
    SELECT ?country ?capital ?countryLabel ?capitalLabel WHERE {
      ?country a dbo:Country ; dbo:capital ?capital .
      ?country rdfs:label ?countryLabel .
      ?capital rdfs:label ?capitalLabel .
      FILTER(LANG(?countryLabel) = "en")
      FILTER(LANG(?capitalLabel) = "en")
    } LIMIT 20
  }

  # 2) Recherche web (Wikipédia) via DuckDuckGo
  BIND(CONCAT("\"", STR(?capitalLabel), "\" capital of ", STR(?countryLabel), " site:wikipedia.org") AS ?q)

  BIND(
    ex:SLM-MCP-TOOL(
      "duckduckgo","duckduckgo.search",
      ex:SLM-OBJ(
        "q", ?q,
        "region","us-en",
        "safe","moderate",
        "limit", 1
      )
    ) AS ?gSearch
  )

  # DuckDuckGo MCP renvoie un DataFeed avec des DataFeedItem, chacun portant schema:item -> WebPage
  GRAPH ?gSearch {
    ?feed a schema:DataFeed ;
      schema:dataFeedElement ?item .
    ?item a schema:DataFeedItem ;
      schema:item ?page .
    ?page a schema:WebPage .
    OPTIONAL { ?page schema:name ?title }
    # L'URL est l'@id de la WebPage (IRI) ; on prévoit un repli si schema:url est fourni
    BIND(STR(?page) AS ?urlIri)
    OPTIONAL { ?page schema:url ?urlProp }
    BIND(COALESCE(STR(?urlProp), ?urlIri) AS ?url)
  }
#  FILTER(CONTAINS(LCASE(STR(?url)), "wikipedia.org"))  # on privilégie Wikipédia

  # 3) Snapshot de la page (texte court, conforme)
  BIND(
    ex:SLM-MCP-TOOL(
      "browser","browser.snapshot",
      ex:SLM-OBJ(
        "url", ?url,
        "wait_ms", 800,
        "wait_until", "networkidle",
        "text_max", 200000,
        "snippet_only", false,
        "redact_pi", true,
        "min_delay_ms", 1200
      )
    ) AS ?gSnap
  )

  GRAPH ?gSnap {
    ?any schema:text ?text .
  }
#      FILTER(BOUND(?text) && STRLEN(STR(?text)) > 50)

  # 4) Vérification par LLM -> ClaimReview JSON-LD
  BIND(
    CONCAT(
      """You are a strict fact-checker. Verify the claim using the provided page text.
Claim: '""", STR(?capitalLabel), " is the capital of ", STR(?countryLabel), """'.
Instructions:
  {"@context":{"@vocab":"https://schema.org/"},"@type":"ClaimReview","claimReviewed":"<claim>","reviewRating":{"@type":"Rating","ratingValue":"0 or 1","bestRating":"1","worstRating":"0","alternateName":"confirmed or refuted"},"description":"short quoted evidence from the page"}
Page text:
<page>
""",
      SUBSTR(?text,1,1800),
      """
</page>
"""
    ) AS ?prompt
  )

  BIND(ex:SLM-LLMGRAPH_GROQ(?prompt, ?url) AS ?gLLM)

  GRAPH ?gLLM {
    ?cr a schema:ClaimReview ;
        schema:claimReviewed ?claim ;
        schema:reviewRating ?rating .
    ?rating a schema:Rating ;
            schema:ratingValue ?ratingValue ;
            schema:alternateName ?decision .
    OPTIONAL { ?cr schema:description ?evidence }
  }
}
ORDER BY ?countryLabel ?capitalLabel