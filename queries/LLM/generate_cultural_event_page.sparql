## slm-run --config config.ini -f queries/generate_cultural_event_page.sparql --debug
##
## Goal: Generate synthetic cultural event pages (instead of scraping).
## Steps:
## 1) Fetch European capitals (Wikidata)
## 2) Fetch cultural event categories (Wikidata)
## 3) Pick a random (city, category)
## 4) Ask the LLM to generate a JSON-LD `schema:Event` (EN) with: name, startDate (YYYY-MM-DD), location, address, maximumAttendeeCapacity, description.
##
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX schema: <http://schema.org/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX slm: <http://example.org/>
PREFIX ex: <http://example.org/>

# Return text extracted from the graph generated by the LLM
SELECT ?cityLabel ?catLabel ?venue ?address ?capacity ?startDate ?text WHERE {
  # 1) Capitales d'Europe
  {
    SERVICE <https://query.wikidata.org/sparql> {
      SELECT ?city ?cityLabel WHERE {
        ?country wdt:P31 wd:Q6256 ; wdt:P30 wd:Q46 ; wdt:P36 ?city .
        ?city rdfs:label ?cityLabel . FILTER(LANG(?cityLabel) = "en")
      } ORDER BY RAND() LIMIT 5
    }
  }

  # 2) Cultural event categories (local list, robust)
  {
    SELECT ?catLabel WHERE {
      VALUES ?catLabel {
        "concert"
        "performance"
        "festival"
        "exhibition"
        "conference"
        "film screening"
      }
    } ORDER BY RAND() LIMIT 1
  }

  # 3) Build the prompt template (EN) in triple quotes and inject city/category
  BIND("""
You are a cultural event content generator.
Create a concise English event description for the city {CITY}, with category {CAT}.
Include in the description: a start date (format YYYY-MM-DD, between 2025-10-01 and 2026-03-31),
the venue name, a simple postal address, the maximum attendee capacity, and a brief 2-sentence summary.

Return ONLY a valid JSON-LD object from schema.org with EXACTLY this shape (no extra fields, no comments, no markdown):
{
  "@context": "http://schema.org/",
  "@type": "CreativeWork",
  "text": "..."
  "capacity": "..."
  "venue": "..."
  "address": "..."
  "startDate": "..."
  },
}
""" AS ?tmpl)

  BIND(REPLACE(REPLACE(?tmpl, "{CITY}", STR(?cityLabel)), "{CAT}", STR(?catLabel)) AS ?prompt)

  # 4) Generate a graph and read schema:text (stable per city)
  BIND(URI(CONCAT("urn:generated:content:", ENCODE_FOR_URI(STR(?cityLabel)))) AS ?doc)
  BIND(ex:SLM-LLMGRAPH_GROQ(?prompt, ?doc) AS ?g)
  GRAPH ?g {
    ?root   schema:text ?text .
    ?root   schema:capacity ?capacity .
    ?root   schema:venue ?venue .
    ?root   schema:address ?address .
    ?root   schema:startDate ?startDate .
    # ?doc ex:has_schema_type ?root .
    # ?root a schema:CreativeWork ;
    #       schema:text ?text .
  }
}
LIMIT 100