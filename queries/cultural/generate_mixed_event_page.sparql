## slm-run --config config.ini -f queries/cultural/generate_mixed_event_page.sparql -o mixed-events.ttl
##
## Goal: Generate synthetic cultural event pages (instead of scraping).
## Steps:
## 1) Fetch European capitals (Wikidata)
## 2) Fetch cultural event categories (Wikidata)
## 3) Pick a random (city, category)
## 4) Ask the LLM to generate a JSON-LD `schema:Event` (EN) with: name, startDate (YYYY-MM-DD), location, address, maximumAttendeeCapacity, description.
##
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX schema: <http://schema.org/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX slm: <http://example.org/>
PREFIX ex: <http://example.org/>

# Return text extracted from the graph generated by the LLM
CONSTRUCT {
  [ a schema:CreativeWork ;
       schema:text ?text ;
       schema:capacity ?capacity ;
       schema:venue ?venue ;
       schema:address ?address ;
       schema:city ?city ;
       schema:cityLabel ?cityLabel ;
       schema:category ?catLabel ;
       schema:type ?type ;
       schema:startDate ?startDate ]
}
#SELECT * 
WHERE {
  # 1) Capitales d'Europe
  {
    SERVICE <https://query.wikidata.org/sparql> {
      SELECT DISTINCT ?city ?cityLabel WHERE {
        ?country wdt:P31 wd:Q6256 ; wdt:P30 wd:Q46 ; wdt:P36 ?city .
        ?city rdfs:label ?cityLabel . FILTER(LANG(?cityLabel) = "en")
      } # LIMIT 5  
    }
  }

  # 2) Cultural event categories (local list, robust)
  {
    SELECT DISTINCT ?type ?catLabel WHERE {
      VALUES (?type ?catLabel) {
        ("cultural" "concert")
        ("cultural" "performance")
        ("cultural" "festival")
        ("cultural" "exhibition")
        ("cultural" "conference")
        ("cultural" "film screening")
        ("science" "physic")
        ("science" "computer science")
        ("science" "biology")
        ("science" "sociology")
        ("science" "anthropology")
        ("science" "mathematics")
      }
    } # ORDER BY RAND() LIMIT 6
  }

  {
    SELECT ?address WHERE {
      VALUES ?address {
        "123 Main St, Anytown"
        "456 Elm St, Othertown"
        "789 Oak St, Sometown"
      }
    } ORDER BY RAND() LIMIT 1
  }

  { 
    SELECT ?capacity WHERE {
      VALUES ?capacity {
        "500"
        "1000"
        "2000"
        "3000"
        "5000"
        "10000"
      }
    } ORDER BY RAND() LIMIT 1
  }

  {
    SELECT ?venue WHERE {
      VALUES ?venue {
        "Grand Theater"
        "City Concert Hall"
        "Downtown Exhibition Center"
        "Main Street Conference Venue"
        "Central Park Amphitheater"
        "Riverside Film Studio"
      }
    } ORDER BY RAND() LIMIT 1
  }

  {
    SELECT ?startDate WHERE {
      VALUES ?startDate {
        "2025-10-15"^^xsd:date
        "2025-11-20"^^xsd:date
        "2025-12-05"^^xsd:date
        "2026-01-10"^^xsd:date
        "2026-02-14"^^xsd:date
        "2026-03-01"^^xsd:date
      }
    } ORDER BY RAND() LIMIT 1
  }


  # 3) Build the prompt template (EN) in triple quotes and inject city/category
  BIND(CONCAT("""
    You are a """, STR(?type), """ event content generator.
    Create an engaging English event description of few lines including : the city """, STR(?cityLabel), 
    ", the category:", STR(?catLabel),", the date:", STR(?startDate), ", the venue:", STR(?venue),
    ", a simple address:", STR(?address), ", the maximum attendee capacity:", STR(?capacity),  
    """
    Return ONLY a valid JSON-LD object from schema.org with EXACTLY this shape (no extra fields, no comments, no markdown):
    {
      "@context": "http://schema.org/",
      "@type": "CreativeWork",
      "text": "..."
      },
    }
    """) AS ?prompt)


  # # 4) Generate a graph and read schema:text (stable per city)
   BIND(URI(CONCAT("urn:generated:content:", ENCODE_FOR_URI(CONCAT(STR(?cityLabel))))) AS ?doc)
   BIND(ex:SLM-LLMGRAPH_GROQ(?prompt, ?doc) AS ?g)
  #
   GRAPH ?g {
     [ schema:text ?text ].
   }
}
